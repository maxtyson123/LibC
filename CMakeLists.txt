# Setup project
CMAKE_MINIMUM_REQUIRED(VERSION 3.31)
PROJECT(LibC LANGUAGES C)

# Make sure a target has been specified
IF(NOT DEFINED LIBC_TARGET)
    message(FATAL_ERROR "You must set -DLIBC_TARGET=<MaxOS|linux|example|...> when configuring.")
ENDIF()

# Validate target
SET(_VALID_LIBC_TARGETS MaxOS example)
LIST(FIND _VALID_LIBC_TARGETS "${LIBC_TARGET}" _found)
IF(_found EQUAL -1)
    MESSAGE(FATAL_ERROR "Unsupported LIBC_TARGET='${LIBC_TARGET}'. Valid values: ${_VALID_LIBC_TARGETS}")
ENDIF()
MESSAGE(STATUS "Platform: ${LIBC_TARGET}")

# Find the sysroot
INCLUDE(GNUInstallDirs)
IF(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
    MESSAGE(FATAL_ERROR "CMAKE_INSTALL_PREFIX is not set. Set it explicitly to a sysroot: -DCMAKE_INSTALL_PREFIX=/path/to/sysroot")
ENDIF()

# Enable assembly
SET(CMAKE_ASM_FLAGS "-f elf64")
SET(CMAKE_ASM_COMPILE_OBJECT "nasm <FLAGS> -o <OBJECT> <SOURCE>")
FILE(GLOB_RECURSE ASM_SRCS src/*.s)
SET_SOURCE_FILES_PROPERTIES(${ASM_SRCS} PROPERTIES LANGUAGE ASM)

# Setup the library
FILE(GLOB_RECURSE LIBC_SOURCES src/*.c src/*.s)
ADD_LIBRARY(c STATIC ${LIBC_SOURCES})
TARGET_COMPILE_DEFINITIONS(c PRIVATE _PLATFORM_${LIBC_TARGET})

# Setup headers for this project and others
TARGET_INCLUDE_DIRECTORIES(c
        PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

# Set the flags
TARGET_COMPILE_OPTIONS(c PRIVATE  -Wall)
TARGET_COMPILE_OPTIONS(c PRIVATE -nostdlib -ffreestanding)

# Install
INSTALL(
        DIRECTORY include/
        DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)
INSTALL(TARGETS c
        EXPORT LibCTargets
        ARCHIVE  DESTINATION ${CMAKE_INSTALL_LIBDIR}
        INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)
INSTALL(EXPORT LibCTargets
        FILE LibCTargets.cmake
        NAMESPACE LibC::
        DESTINATION "${CMAKE_INSTALL_LIBDIR}/cmake/LibC"
)


#TODO: CMakePackageconfig, and uninstall option